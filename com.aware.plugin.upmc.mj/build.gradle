apply plugin: 'com.android.application'

android {
    compileSdkVersion compile_sdk
    buildToolsVersion build_tools

    defaultConfig {
        applicationId "com.aware.plugin.upmc.mj" //EDIT ME
        minSdkVersion minimum_sdk
        targetSdkVersion target_sdk
        versionCode version_code
        versionName version_readable
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi'
        }
    }

    lintOptions {
        abortOnError false
    }
}

if (System.getenv("storeFile") != null && System.getenv("storePassword") != null && System.getenv("keyAlias") != null && System.getenv("keyPassword") != null) {
    android.signingConfigs.release.storeFile = file(System.getenv("storeFile"))
    android.signingConfigs.release.storePassword = System.getenv("storePassword")
    android.signingConfigs.release.keyAlias = System.getenv("keyAlias")
    android.signingConfigs.release.keyPassword = System.getenv("keyPassword")
} else {
    println("No keystore defined. Unsigned!")
    android.buildTypes.release.signingConfig = null
}

repositories {
    maven { url 'https://jitpack.io' } //this allows library generation directly from GitHub source-code
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {

    compile fileTree(dir: 'libs', include: ['*.jar'])

    //AWARE dependencies
    implementation "org.eclipse.paho:org.eclipse.paho.client.mqttv3:$mqtt_libs"
    implementation "com.koushikdutta.ion:ion:$ion_libs"
    implementation "com.android.support:appcompat-v7:$support_libs"
    implementation "com.android.support:gridlayout-v7:$support_libs"

    //AWARE core library
    api 'com.github.denzilferreira:aware-client:development-SNAPSHOT'

    //plugins' dependencies...
    implementation 'com.github.scribejava:scribejava-apis:4.2.0'
    implementation 'net.danlew:android.joda:2.9.5.1'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.2'
    implementation "com.google.android.gms:play-services-location:$google_libs"

    //bundled plugins
    implementation 'com.github.denzilferreira:com.aware.plugin.google.activity_recognition:master-SNAPSHOT'
    implementation 'com.github.denzilferreira:com.aware.plugin.device_usage:master-SNAPSHOT'
    implementation 'com.github.denzilferreira:com.aware.plugin.google.fused_location:master-SNAPSHOT'
    implementation 'com.github.denzilferreira:com.aware.plugin.fitbit:master-SNAPSHOT'
    implementation 'com.github.denzilferreira:com.aware.plugin.studentlife.audio_final:master-SNAPSHOT' //conversations

}
